// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:

    // if (zx==1) set x = 0
    Mux16(a=x, b[0..15]=false, sel=zx, out=ozx);

    // if (nx==1) set x = ~x
    Not16(in=ozx, out=nozx);
    Mux16(a=ozx, b=nozx, sel=nx, out=onx);

    // if (zy==1) set y = 0
    Mux16(a=y, b[0..15]=false, sel=zy, out=ozy);

    // if (ny==1) set y = ~y
    Not16(in=ozy, out=nozy);
    Mux16(a=ozy, b=nozy, sel=ny, out=ony);

    // if (f==1)  set out = x + y
    // if (f==0)  set out = x & y
    Add16(a=onx, b=ony, out=addxy);
    And16(a=onx, b=ony, out=andxy);
    Mux16(a=andxy, b=addxy, sel=f, out=of);

    // if (no==1) set out = ~out
    Not16(in=of, out=nof);
    Mux16(a=of, b=nof, sel=no, out=ono);

    // zr, if (out == 0) equals 1, else 0
    IsZero16(in=ono, out=zr);

    // ng, if (out < 0)  equals 1, else 0
    Or16(a=ono, b[0..15]=false, out=out, out[15]=ng);

}
